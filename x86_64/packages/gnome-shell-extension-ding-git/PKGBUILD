pkgname=gnome-shell-extension-ding-git
pkgdesc="Desktop Icons NG - add icons to the desktop."
pkgver=r1079.3a5cc11
pkgrel=1
_gitname=desktop-icons-ng
arch=('any')
url="https://gitlab.com/rastersoft/desktop-icons-ng"
license=("GPLv3")
install="ding.install"
depends=('gnome-shell' 'file-roller' 'nautilus')
makedepends=('meson' 'ninja' 'git' 'unzip')

_dir="${_gitname}"
source=("git+https://gitlab.com/rastersoft/desktop-icons-ng.git")
sha256sums=("SKIP")

pkgver() {
    cd "${srcdir}/${_dir}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_dir}"
    ./export-zip.sh
}

package() {
    mkdir -p "${pkgdir}/usr/share/gnome-shell/extensions/ding@rastersoft.com"

    unzip "${srcdir}/${_dir}/ding@rastersoft.com.zip" -d "${pkgdir}/usr/share/gnome-shell/extensions/ding@rastersoft.com/"

    install -Dm644 "${srcdir}/${_dir}/schemas/org.gnome.shell.extensions.ding.gschema.xml" -t "$pkgdir/usr/share/glib-2.0/schemas"
}
