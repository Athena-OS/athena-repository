pkgname=athena-seclists
_pkgname=SecLists
pkgver=1552.decd3cb
pkgrel=1
pkgdesc='A collection of multiple types of lists used during security assessments.'
arch=('any')
url='https://github.com/danielmiessler/SecLists'
license=('MIT')
depends=()
makedepends=('git')
source=("git+https://github.com/danielmiessler/$_pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $_pkgname

  install -dm 755 "$pkgdir/usr/share/payloads/$_pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$_pkgname/" *.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"

  rm *.md LICENSE

  find -maxdepth 1 | grep -v \\.git |
  xargs -I{} cp -r {} "$pkgdir/usr/share/payloads/$_pkgname"
}